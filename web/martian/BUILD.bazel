load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary")
load("@bazel_skylib//rules:select_file.bzl", "select_file")

genrule(
    name = "clean_css",
    srcs = [
        "res/css/main.css",
    ],
    outs = [
        "build/main.css",
    ],
    cmd = "$(execpath @martian_npm//clean-css-cli/bin:cleancss) " +
          "-o $(execpath build/main.css) " +
          "$(execpath res/css/main.css)",
    tools = [
        "@martian_npm//clean-css-cli/bin:cleancss",
    ],
)

nodejs_binary(
    name = "htmlmin_bin",
    data = ["@martian_npm//html-minifier-terser"],
    entry_point = "@martian_npm//:node_modules/html-minifier-terser/cli.js",
)

genrule(
    name = "htmlmin",
    srcs = [
        "templates/graph.html",
    ],
    outs = [
        "serve/graph.html",
    ],
    cmd = "$(execpath :htmlmin_bin) " +
          "-o $@ " +
          "--remove-comments --collapse-whitespace " +
          "--continue-on-parse-error --minify-css true " +
          "--minify-js '{ \"mangle\": false }' " +
          "$<",
    tools = [
        ":htmlmin_bin",
    ],
)

select_file(
    name = "d3",
    srcs = "@martian_npm//d3:d3__files",
    subpath = "d3.min.js",
)

select_file(
    name = "dagre_d3",
    srcs = "@martian_npm//dagre-d3:dagre-d3__files",
    subpath = "dagre-d3.min.js",
)

select_file(
    name = "angular",
    srcs = "@martian_npm//angular:angular__files",
    subpath = "angular.min.js",
)

select_file(
    name = "lodash",
    srcs = "@martian_npm//lodash:lodash__files",
    subpath = "lodash.min.js",
)

select_file(
    name = "ng_google_chart",
    srcs = "@martian_npm//angular-google-chart:angular-google-chart__files",
    subpath = "ng-google-chart.min.js",
)

select_file(
    name = "ui_bootstrap_tpls",
    srcs = "@martian_npm//angular-ui-bootstrap:angular-ui-bootstrap__files",
    subpath = "ui-bootstrap-tpls.min.js",
)

nodejs_binary(
    name = "terser_bin",
    data = ["@martian_npm//terser"],
    entry_point = "@martian_npm//:node_modules/terser/bin/terser",
)

# Merge the js together.
genrule(
    name = "merged_js",
    srcs = [
        ":d3",
        ":dagre_d3",
        ":angular",
        ":ui_bootstrap_tpls",
        ":lodash",
        ":ng_google_chart",
        "client/graph.js",
    ],
    outs = [
        "serve/graph.js",
    ],
    cmd = "cat $(SRCS) | " +
          "$(execpath :terser_bin) | " +
          "gzip --best > \"$@\"",
    tools = [
        ":terser_bin",
    ],
)

COMPRESS = "gzip --best -c $$(readlink -f $<) > $@"

genrule(
    name = "compress_css",
    srcs = [
        ":clean_css",
    ],
    outs = [
        "serve/css/main.css",
    ],
    cmd = COMPRESS,
)

genrule(
    name = "compress_favicon",
    srcs = [
        "res/favicon.ico",
    ],
    outs = [
        "serve/favicon.ico",
    ],
    cmd = COMPRESS,
)

filegroup(
    name = "martian",
    srcs = [
        ":compress_css",
        ":compress_favicon",
        ":htmlmin",
        ":merged_js",
    ],
    visibility = ["//visibility:public"],
)
